{
  parserClass="gdscript.parser.GdParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Gd"
  psiImplClassSuffix="Impl"
  psiPackage="gdscript.psi"
  psiImplPackage="gdscript.psi.impl"

  elementTypeHolderClass="gdscript.psi.GdTypes"
  elementTypeClass="gdscript.psi.GdElementType"
  tokenTypeClass="gdscript.psi.GdTokenType"

  psiImplUtilClass="gdscript.psi.GdPsiUtils"

  mixin(".*_nm")="GdNamedElementImpl"
  implements(".*_nm")="GdNamedElement"
  methods(".*_nm")=[getName setName]

  mixin(".*_nmi")="GdNamedIdElementImpl"
  implements(".*_nmi")="GdNamedIdElement"
  methods(".*_nmi")=[getName setName getNameIdentifier]

  extends(".*_tl")=topLevelDecl
  extends(".*_ex")=expr
  extends(".*_st")=stmt

  consumeTokenMethod(".*_ex|expr|stmt|.*_st|topLevelDecl|.*_tl")="consumeTokenFast"

  tokens = [
    string='regexp:"[^"]*?"'
    number='regexp:[0-9]+(\.)?[0-9]*'
    comment='regexp:#.*'
    bad_character='regexp:[\^]'
  ]
}

gdfile ::= (inheritance | classNaming)* topLevelDecl* // TODO limit to once
//TODO DOT IDENTIFIER -> inner class?
inheritance ::= EXTENDS inheritanceId_nmi (DOT IDENTIFIER)? newLineEnd {methods=[getInheritanceName] pin=1 recoverWhile=inheritance_r mixin="gdscript.psi.impl.GdInheritanceElementImpl" stubClass="gdscript.index.stub.GdInheritanceStub" elementTypeFactory="gdscript.psi.impl.GdInheritanceElementType.getInstance"}
inheritanceId_nmi ::= IDENTIFIER
private inheritance_r ::= !(EXTENDS | CLASS_NAME | TOOL) & topLevelDecl_r

classNaming ::= CLASS_NAME className_nm (COMMA STRING)? newLineEnd {methods=[getClassname getParentName] pin=1 recoverWhile=inheritance_r mixin="gdscript.psi.impl.GdClassNamingElementImpl" stubClass="gdscript.index.stub.GdClassNamingStub" elementTypeFactory="gdscript.psi.impl.GdClassNamingElementType.getInstance"}
className_nm ::= IDENTIFIER

topLevelDecl ::=
    constDecl_tl
    | enumDecl_tl
    | signalDecl_tl
    | classVarDecl_tl
    | annotation_tl
    | methodDecl_tl
private topLevelDecl_r ::= !(FUNC | CONST | SIGNAL | VAR | ENUM | ANNOTATOR | IDENTIFIER | CLASS_NAME | STATIC | VARARG | RRBR | RCBR | RSBR)

constDecl_tl ::= CONST constId_nmi typed? assignTyped expr endStmt {methods=[getConstName getReturnType getPresentation] pin=1 recoverWhile=topLevelDecl_r mixin="gdscript.psi.impl.GdConstDeclElementImpl" stubClass="gdscript.index.stub.GdConstDeclStub" elementTypeFactory="gdscript.psi.impl.GdConstDeclElementType.getInstance"}
constId_nmi ::= IDENTIFIER | INF;

annotation_tl ::= ANNOTATOR (LRBR literal_ex (COMMA literal_ex)* RRBR)? {pin=1 recoverWhile=topLevelDecl_r}
classVarDecl_tl ::= VAR classVarId_nmi typed? (assignTyped expr)? (setgetDecl | endStmt) {methods=[getName getReturnType getPresentation] pin=1 recoverWhile=topLevelDecl_r mixin="gdscript.psi.impl.GdClassVarDeclElementImpl" stubClass="gdscript.index.stub.GdClassVarDeclStub" elementTypeFactory="gdscript.psi.impl.GdClassVarDeclElementType.getInstance"}
classVarId_nmi ::= IDENTIFIER

setgetDecl ::= COLON (NEW_LINE INDENT)? (getDecl | setDecl)+ DEDENT?
getDecl ::= GET ((COLON stmtOrSuite) | (EQ getMethodId_nm COMMA? NEW_LINE?))
setDecl ::= SET ((LRBR var_nmi typed? RRBR COLON stmtOrSuite) | (EQ setMethodId_nm COMMA? NEW_LINE?))
setMethodId_nm ::= IDENTIFIER
getMethodId_nm ::= IDENTIFIER

signalDecl_tl ::= SIGNAL signalId_nmi (LRBR paramList RRBR)? endStmt {methods=[getName getParameters] pin=1 recoverWhile=topLevelDecl_r mixin="gdscript.psi.impl.GdSignalDeclElementImpl" stubClass="gdscript.index.stub.GdSignalDeclStub" elementTypeFactory="gdscript.psi.impl.GdSignalDeclElementType.getInstance"}
signalId_nmi ::= IDENTIFIER

enumDecl_tl ::= ENUM enumDecl_nmi? LCBR enumValue ((COMMA | NEW_LINE) enumValue)* (COMMA | NEW_LINE)? RCBR endStmt {pin=1 methods=[getValues getPresentation] recoverWhile=topLevelDecl_r mixin="gdscript.psi.impl.GdEnumDeclElementImpl" stubClass="gdscript.index.stub.GdEnumDeclStub" elementTypeFactory="gdscript.psi.impl.GdEnumDeclElementType.getInstance"}
enumDecl_nmi ::= IDENTIFIER
enumValue ::= enumValue_nmi (EQ (PLUS | MINUS)? NUMBER)?
enumValue_nmi ::= IDENTIFIER

// TODO recover nesmí mít func -> inline lambdy by to porušily
methodDecl_tl ::= STATIC? VARARG? FUNC methodId_nmi LRBR paramList? RRBR parentMethodCall? returnHint? COLON stmtOrSuite {methods=[isStatic isVariadic getName getReturnType getParameters getPresentation isConstructor] pin=3 recoverWhile=topLevelDecl_r mixin="gdscript.psi.impl.GdMethodDeclElementImpl" stubClass="gdscript.index.stub.GdMethodDeclStub" elementTypeFactory="gdscript.psi.impl.GdMethodDeclElementType.getInstance"}
methodId_nmi ::= IDENTIFIER | GET | SET
paramList ::= param (COMMA param)* {pin=1 recoverWhile=argList_r}
param ::= VAR? var_nmi typed? (EQ expr)? {methods=[getReturnType]}
returnHint ::= RET returnHintVal
returnHintVal ::= typedVal | VOID // typeHint_nm | VOID
parentMethodCall ::= DOT LRBR argList? RRBR
argList ::= expr (COMMA expr)* {pin=1 recoverWhile=argList_r}
private argList_r ::= !(RRBR)

// func - Array.gd, signal: Vector2.gd, class_name - Array.gd
var_nmi ::= IDENTIFIER | SIGNAL | FUNC | CLASS_NAME // TODO tyhle vyjímky jsou kvůli parseru sdk -> nějaké params se shodují jmenovitě

// TODO inner class

stmtOrSuite ::= stmt | suite
suite ::= NEW_LINE? INDENT stmt+ DEDENT {pin=2}

stmt ::=
    assign_st
    | parent_st
    | varDecl_st
    | constDecl_st
    | if_st
    | while_st
    | for_st
    | match_st
    | flow_st
    | assert_st
    | await_st
    | preload_st
    | expr_st
    | (expr endStmt)
private stmt_r ::= !(SET | GET | VAR | CONST | IF | PASS | CONTINUE | BREAK | BREAKPOINT | WHILE | FOR | MATCH | RETURN | AWAIT | ASSET | PRELOAD | INDENT | DEDENT | IDENTIFIER | literal_ex)

parent_st ::= DOT call_ex endStmt?
varDecl_st ::= VAR var_nmi typed? (assignTyped expr)? endStmt {pin=2 recoverWhile=stmt_r methods=[getName getReturnType]}
constDecl_st ::= CONST var_nmi typed? (assignTyped expr)? endStmt {pin=2 recoverWhile=stmt_r methods=[getName getReturnType]}
if_st ::= IF expr COLON stmtOrSuite (ELIF expr COLON stmtOrSuite)* (ELSE COLON stmtOrSuite)? {pin=2 recoverWhile=stmt_r}
while_st ::= WHILE expr COLON stmtOrSuite {pin=2 recoverWhile=stmt_r}
for_st ::= FOR var_nmi IN expr COLON stmtOrSuite {pin=2 recoverWhile=stmt_r}

match_st ::= MATCH expr COLON NEW_LINE INDENT matchBlock DEDENT {pin=1 recoverWhile=stmt_r}
matchBlock ::= (patternList COLON stmtOrSuite)+
patternList ::= pattern (COMMA pattern)*
//TODO (* Note: you can't have a binding in a pattern list, but to not complicate the grammar more it won't be restricted syntactically *)
// Asi annotátor
pattern ::= literal_ex | builtInType | UNDER | bindingPattern | arrayPattern | dictPattern
bindingPattern ::= VAR var_nmi
arrayPattern ::= LSBR [ pattern { COMMA pattern } [ DOTDOT ] ] RSBR
dictPattern ::= LCBR [ keyValuePattern ] { COMMA keyValuePattern } [ DOTDOT ] RCBR
keyValuePattern ::= STRING [ COLON pattern ]

assert_st ::= ASSERT LRBR expr (COMMA STRING)? RRBR endStmt {pin=1 recoverWhile=stmt_r}
await_st ::= AWAIT expr_st {pin=1 recoverWhile=stmt_r}
preload_st ::= PRELOAD LRBR STRING RRBR {pin=2 recoverWhile=stmt_r}

assign_st ::= expr (EQ | ASSIGN) expr endStmt //{pin=2 recoverWhile=stmt_r}
expr_st ::= expr endStmt {pin=1 recoverWhile=stmt_r}

flow_st ::=
    (CONTINUE endStmt)
    | (BREAK endStmt)
    | (PASS endStmt)
    | (BREAKPOINT endStmt)
    | returnStmt
returnStmt ::= RETURN expr? endStmt {pin=1 /*recoverWhile=stmt_r*/}

expr ::=
    plusMinus_ex
    | attribute_ex
    | cast_ex
    | arr_ex
    | call_ex
    | ternary_ex
    | logic_ex
    | negate_ex
    | in_ex
    | comparison_ex
    | bitAnd_ex
    | shift_ex
    | plus_ex
    | factor_ex
    | sign_ex
    | bitNot_ex
    | plusMinusPre_ex
    | is_ex
    | primary_ex
    | literal_ex
    {methods=[getReturnType]}

cast_ex ::= expr AS isTyped
ternary_ex ::= expr IF expr ELSE expr
logic_ex ::= expr (ANDAND | OROR) expr
negate_ex ::= NEGATE expr;
in_ex ::= expr IN expr
comparison_ex ::= expr TEST_OPERATOR expr
bitAnd_ex ::= expr (AND | XOR | OR) expr
shift_ex ::= expr (LBSHIFT | RBSHIFT) expr
plus_ex ::= expr (PLUS | MINUS) expr
factor_ex ::= expr (MUL | DIV | MOD) expr
sign_ex ::= (MINUS | PLUS ) expr
bitNot_ex ::= NOT expr
is_ex ::= expr IS isTyped
call_ex ::= expr LRBR argList? RRBR
arr_ex ::= expr LSBR expr RSBR
primary_ex ::= nodePath | arrayDecl | dictDecl | (LRBR expr RRBR)
literal_ex ::= TRUE | FALSE | STRING | NUMBER | NULL | PI | TAU | NAN | INF | refId_nm
plusMinus_ex ::= expr (PPLUS | MMINUS)
plusMinusPre_ex ::= (PPLUS | MMINUS) expr
attribute_ex ::= expr DOT expr
refId_nm ::= IDENTIFIER | SELF

//@"Node/Label"
//NodePath or StringName

isTyped ::= typeHint_nm (LSBR typeHintArray_nm RSBR)?
typed ::= COLON typedVal
private typedVal ::= typeHint_nm (LSBR typeHint_nm RSBR)?
typeHint_nm ::= builtInType | IDENTIFIER
typeHintArray_nm ::= builtInType | IDENTIFIER
builtInType ::= INT | STR | FLOAT | BOOL
endStmt ::= SEMICON | newLineEnd
newLineEnd ::= NEW_LINE | <<eof>>
nodePath ::= NODE_PATH_LEX
//private id_r ::= !(builtInType | endStmt | refId_nm | COLON | ASSERT | ASSIGN | NEW_LINE | '=') & stmt_r

arrayDecl ::= LSBR (expr (COMMA expr)* COMMA?)? RSBR
dictDecl ::= LCBR (keyValue (COMMA keyValue)* COMMA?)? RCBR
keyValue ::= (expr COLON expr) | (IDENTIFIER EQ expr)

assignTyped ::= EQ | CEQ